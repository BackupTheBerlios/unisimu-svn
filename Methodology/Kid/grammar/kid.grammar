<autotree>

program: statement_list eofile
       | <error>

statement_list: statement(s)

statement: if_statement
         | assignment /;?/
         | <error>

if_statement: 'if' <commit> '(' condition ')' block <uncommit>
              ('else' <commit> else_block)(?)
            | <error?> <reject>

condition: expression rel_op <commit> rhs_expression
         | <error?> <reject>

rel_op: />=|>|<=|<>|<|=/

rhs_expression: expression

else_block: block

block: '{' <commit> statement_list '}'
     | <error?> <reject>

assignment: var ':=' <commit> expression
          | <error?> <reject>

var: identifier

identifier: /[A-Za-z][A-Za-z0-9_]*/

expression: <leftop: term /([-+])/ term>

term: <leftop: factor /([*\/])/ factor>
    | neg <leftop: factor /([*\/])/ factor>

neg: '-'

factor: var
      | number
      | '(' <commit> expression ')'

number: /-?\d+(?:\.\d+)?/

eofile: /^\Z/
